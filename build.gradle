plugins {
    id 'antlr'
    id 'java-library'
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'io.corrlang.gqlintegration.syntax.parser']
    outputDirectory = new File("src/generated/io/corrlang/gqlintegration/syntax/parser")
}

configurations {
    directDependencies
}


dependencies {
    directDependencies 'com.graphql-java:graphql-java:16.1'
    implementation configurations.directDependencies
    implementation project(':mdegraphlib')
    implementation project(':corr-lang-core')
    testImplementation project(':mdegraphlib').sourceSets.test.output
    implementation 'org.antlr:antlr4-runtime:4.8'
    antlr "org.antlr:antlr4:4.8"

}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        java.srcDir 'src/generated'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
}

clean {
    delete 'src/generated'
}

test {
    filter {
        includeTestsMatching "io.corrlang.gqlintegration.GraphQLTestsuite*"
    }
}

task exportDependencies(type: Copy) {
    from configurations.directDependencies
    into "${buildDir}/extlibs"
}